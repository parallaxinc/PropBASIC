' ----------------------------------------------------------------------
' GLOBAL: Device Settings
' ----------------------------------------------------------------------
DEVICE          P8X32A, XTAL1, PLL16X
XIN             5_000_000

' ----------------------------------------------------------------------
' GLOBAL: I/O Pins
' ----------------------------------------------------------------------
TX_PIN     PIN   30 INPUT
RX_PIN     PIN   31 INPUT

' ----------------------------------------------------------------------
' GLOBAL: Constants
' ----------------------------------------------------------------------
' -- !!! You HAVE TO use Variables for the BAUD Rate/s
Terminal_Baud   CON   115200

' -- "TX_RX_Format_Lib_.pbas" Constants ---------------
HOME        CON   1
CR          CON   13
CLS         CON   16   

' ----------------------------------------------------------------------
' GLOBAL: HUB Variables (Byte, Word, Long) - use RDxxxx and WRxxxx
' ----------------------------------------------------------------------

Hub_Array   HUB   WORD(120)   ' (2 x 3 x 4 x 5) Single Dimension Array to hold Multi-Dimensional Array Values 


' ----------------------------------------------------------------------
' GLOBAL: DATA, WDATA, LDATA, FILE - use RDxxxx and WRxxxx
' ----------------------------------------------------------------------


' ----------------------------------------------------------------------
' COG 0: Variables (Long only)
' ----------------------------------------------------------------------
TX_BAUD   VAR   LONG
RX_BAUD   VAR   LONG


Xe          CON   2     ' Number of Elements in X Dimension
Ye          CON   3     ' Number of Elements in Y Dimension
Ze          CON   4     ' Number of Elements in Z Dimension
Te          CON   5     ' Number of Elements in T Dimension

X_Loop      CON   (Xe - 1)     ' Number of Elements Loop limit for X Dimension
Y_Loop      CON   (Ye - 1)     ' Number of Elements Loop limit for Y Dimension
Z_Loop      CON   (Ze - 1)     ' Number of Elements Loop limit for Z Dimension
T_Loop      CON   (Te - 1)     ' Number of Elements Loop limit for T Dimension

' Array       VAR   LONG(120)  ' (2 x 3 x 4 x 5) Single Dimension Array to hold Multi-Dimensional Array Values 

Xi          VAR   LONG     ' Calculated X Index for the Array
Yi          VAR   LONG     ' Calculated Y Index for the Array
Zi          VAR   LONG     ' Calculated Z Index for the Array

Idx         VAR   LONG     ' Calculated Index for the Single Dimension Array

Xc          VAR   LONG     ' X Co-ordinate
Yc          VAR   LONG     ' Y Co-ordinate
Zc          VAR   LONG     ' Z Co-ordinate
Tc          VAR   LONG     ' T Co-ordinate

Temp        VAR   LONG



' ----------------------------------------------------------------------
' COG 0: SUB & FUNC Definitions
' ----------------------------------------------------------------------
LOAD "D:\Propeller\Propeller Programs\Libraries\DELAYS_LIB.pbas"
LOAD "D:\Propeller\Propeller Programs\Libraries\TX_RX_LIB.pbas"

Calc_4_Dim_Index     SUB   0
Write_Array    SUB   1
Read_Array     FUNC  0




' ----------------------------------------------------------------------
' TASK Definitions
' ----------------------------------------------------------------------


' ======================================================================
  PROGRAM START
' ======================================================================

START:

   TX_BAUD = Terminal_Baud
   RX_BAUD = Terminal_Baud
   
   DELAY_MS 3000
   TX_BYTE CLS
   DELAY_MS 1000

   
' -- Clear the Array 
   FOR Xc = 0 TO X_Loop
   
      FOR Yc = 0 TO Y_Loop  

          FOR Zc = 0 TO Z_Loop
          
            FOR Tc = 0 TO T_Loop
         
               GOSUB Write_Array, 0    ' Value
            
            NEXT
               
          NEXT
    
      NEXT
 
   NEXT
   
   
   
   
' -- Write to the Array   
   FOR Xc = 0 TO X_Loop
   
      FOR Yc = 0 TO Y_Loop  

         FOR Zc = 0 TO Z_Loop
         
            FOR Tc = 0 TO T_Loop
         
               GOSUB Calc_4_Dim_Index
               
               TX_DEC Xc
               TX_BYTE " "
               TX_DEC Yc
               TX_BYTE " "
               TX_DEC Zc
               TX_BYTE " "
               TX_DEC Tc
               TX_BYTE " "
               TX_DEC Idx
               TX_BYTE CR
            
               GOSUB Write_Array, Idx    ' Value 

            NEXT
         
         NEXT
    
      NEXT
 
   NEXT
               
               
   TX_BYTE CR
   TX_BYTE CR
   
   
   
' -- Read from the Array    
   FOR Xc = 0 TO X_Loop
   
      FOR Yc = 0 TO Y_Loop  

         FOR Zc = 0 TO Z_Loop
         
            FOR Tc = 0 TO T_Loop
         
               TX_DEC Xc
               TX_BYTE " "
               TX_DEC Yc
               TX_BYTE " "
               TX_DEC Zc
               TX_BYTE " "
               TX_DEC Tc
               TX_BYTE " "
            
               Temp = Read_Array
               
               TX_DEC Temp
               TX_BYTE CR
               
            NEXT
                
         NEXT
    
      NEXT
 
   NEXT


END


' ----------------------------------------------------------------------
' COG 0: SUB & FUNC Code
' ----------------------------------------------------------------------

' ----------------------------------------------------------------------
SUB  Calc_4_Dim_Index
   
   ' Xi = Xc * Ye * Ze * Te
   Xi = Xc * Ye
   Xi = Xi * Ze
   Xi = Xi * Te
   
   ' Yi = Yc * Ze * Te
   Yi = Yc * Ze
   Yi = Yi * Te
   
   ' Zi = Zc * Te
   Zi = Zc * Te
   
   ' Idx = Xi + Yi + Zi + Tc
   Idx = Xi + Yi
   Idx = Idx + Zi
   Idx = Idx + Tc

ENDSUB





' ----------------------------------------------------------------------
SUB  Write_Array

   GOSUB Calc_4_Dim_Index
  
   ' Array(Idx) = __param1 
   
   WRWORD Hub_Array(Idx), __param1

ENDSUB



' ----------------------------------------------------------------------
FUNC  Read_Array

   GOSUB Calc_4_Dim_Index
  
   ' __param1 = Array(Idx)
   
   RDWORD Hub_Array(Idx), __param1

   RETURN __param1

ENDFUNC



